plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.3'
    id 'io.spring.dependency-management' version '1.1.6'
    id "org.asciidoctor.jvm.convert" version "3.3.2" //REST Docs

}

group = 'jeiu.capstone'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

// querydsl Q class 설정
def generated = 'src/main/generated'

tasks.register('createGeneratedDir') {
    doLast {
        file(generated).mkdirs()
    }
}

compileJava.dependsOn createGeneratedDir

sourceSets {
    main.java.srcDirs += [ generated ]
}

tasks.withType(JavaCompile) {
    options.annotationProcessorGeneratedSourcesDirectory = file(generated)
}

clean.doLast {
    file(generated).deleteDir()
}
// querydsl Q class 설정

configurations {
    asciidoctorExt //REST Docs

    compileOnly {
        extendsFrom annotationProcessor
    }
}

//REST Docs
ext {
    snippetsDir = file('build/generated-snippets')
}

test {
    outputs.dir snippetsDir
}

asciidoctor {
    inputs.dir snippetsDir
    configurations 'asciidoctorExt'
    dependsOn test
}

bootJar {
    dependsOn asciidoctor

    copy {
        from asciidoctor.outputDir
        into "src/main/resources/static/docs"
    }
}
//REST Docs

repositories {
    mavenCentral()
}

dependencies {

    //Spring Cloud - AWS SDK
    implementation 'io.awspring.cloud:spring-cloud-aws-starter-s3:3.2.0'

    //Spring WEB
    implementation 'org.springframework.boot:spring-boot-starter-web'

    //Spring Validation
    implementation 'org.springframework.boot:spring-boot-starter-validation' //null, 빈값 검증을 위해 필요

    //Spring Security
    implementation 'org.springframework.boot:spring-boot-starter-security'
    testImplementation 'org.springframework.security:spring-security-test'

    //JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.6'

    //JPA
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    // JPA 로깅시 ? 내용을 표시하기 위한 p6spy 라이브라리
    implementation 'p6spy:p6spy:3.9.1'
    implementation 'com.github.gavlyukovskiy:datasource-decorator-spring-boot-autoconfigure:1.9.0'



    // queryDSL
    implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
    annotationProcessor "com.querydsl:querydsl-apt:5.0.0:jakarta"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"

    //lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    //DB Connector
    runtimeOnly 'com.mysql:mysql-connector-j'

    //REST Docs
    asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor:3.0.1'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc:3.0.1'

    //Test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // 이메일 발송을 위한 의존성
    implementation 'org.springframework.boot:spring-boot-starter-mail'
    
    // 유효성 검사를 위한 의존성
    implementation 'org.springframework.boot:spring-boot-starter-validation'
}

tasks.named('test') {
    useJUnitPlatform()
}